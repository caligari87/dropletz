version "3.3"

class DZ_BloodDrop : Actor {
	int testscale;

	default {
		radius 0.5;
		height 1.0;
		+MISSILE;
		+NOTONAUTOMAP;
		+FORCEXYBILLBOARD;
		+NOTELEPORT;
		+BLOODLESSIMPACT;
		+FLOORCLIP;
		+WINDTHRUST;
		+NOBLOCKMAP;
		+THRUACTORS;
		+MOVEWITHSECTOR;
	}

	states {
		spawn:
			DBLD # 1 DZA_RandomScale();
			loop;
		death:
			DBLD # 1 A_SpawnItemEx("DZ_FlatBloodSplat");
			stop;
	}

	action void DZA_RandomScale() {
		invoker.testscale = randompick(1,2);
		if(invoker.testscale==1) { frame = 0; }
		if(invoker.testscale==2) { frame = randompick(1,2); }
	}
}

class DZ_FlatBloodSplat : Actor {
	int testscale;

	default {
		+THRUACTORS;
		+FLATSPRITE;
		+NOBLOCKMAP;
		+MOVEWITHSECTOR;
		+NOTELEPORT;
	}

	states {
		spawn:
			TNT1 A 0;
			DBLD A -1 DZA_FixPosition();
			stop;
	}

	action void DZA_FixPosition() {
		invoker.testscale = randompick(1,2,3);
		angle = randompick(0,1,2,3) * 90;
		SetOrigin((int(pos.x), int(pos.y), int(pos.z)), false);
		if(invoker.testscale==1) { frame = 0; }
		if(invoker.testscale==2) { frame = randompick(1,2);	}
		if(invoker.testscale==3) { frame = randompick(3,8); }
	}
}

class DZ_BloodSpurt : Actor replaces Blood {
	int spraystrength;

	default {
	}

	states {
		spawn:
			TNT1 A 0;
			TNT1 AAAAAAAAAA 1 DZA_SprayBloodDrops();
			stop;
	}

	action void DZA_SprayBloodDrops() {
		A_SpawnItemEx("DZ_BloodDrop",
			0, 0, 0,
			frandom(invoker.spraystrength*0.1, invoker.spraystrength*1), frandom(-1, 1), frandom(-1, 1));
		invoker.spraystrength++;
		angle += frandom(1,3);
		if(invoker.spraystrength>=10) {
			invoker.spraystrength = 1;
			angle += frandom(-30,30);
		}
	}
}